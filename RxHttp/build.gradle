apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

version = "1.0.8"
android {
    compileSdkVersion Config.compileSdkVersion

    defaultConfig {
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode 8
        versionName version
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility Config.sourceCompatibilityVersion
        targetCompatibility Config.targetCompatibilityVersion
    }

    lintOptions{
        abortOnError false
    }

}

dependencies {

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation AndroidX.appCompat
    testImplementation BuildDependencies.junit
    api BuildDependencies.rxjava2
    api BuildDependencies.rxAndroid
    api BuildDependencies.retrofit
    api BuildDependencies.typeBuilder
    api BuildDependencies.retrofitConverterScalars
    api BuildDependencies.retrofitConverterGson
    api BuildDependencies.retrofitAdapterRxjava2
    api BuildDependencies.autodispose
    api BuildDependencies.loggingInterceptor
}

def siteUrl = 'https://github.com/kongpf8848/RxHttp'
def gitUrl = 'https://github.com/kongpf8848/RxHttp.git'
def libraryDescription= 'Http library base on Retrofit and RxJava2'
group = "com.github.kongpf8848"
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                artifactId 'RxHttp'
                version  "$version"
                name 'RxHttp'
                description libraryDescription
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name 'kongpf'
                        email 'kongpf8848@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet "UTF-8"
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir

}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "RxHttp"
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            desc = libraryDescription
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}